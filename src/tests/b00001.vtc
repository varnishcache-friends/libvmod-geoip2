varnishtest "Test various lookups"

server s1 {
	rxreq
	txresp
} -start

shell "cp ${vmod_topsrc}/maxmind-db/test-data/*.mmdb ${tmpdir}"

varnish v1 -vcl+backend {
	import geoip2 from "${vmod_topbuild}/src/.libs/libvmod_geoip2.so";
	import std;

	sub vcl_init {
		new db1 = geoip2.geoip2(
		    "${tmpdir}/GeoIP2-City-Test.mmdb");
		new db2 = geoip2.geoip2(
		    "${tmpdir}/GeoIP2-Connection-Type-Test.mmdb");
		new db3 = geoip2.geoip2(
		    "${tmpdir}/GeoIP2-ISP-Test.mmdb");
	}

	sub vcl_deliver {
		set resp.http.city = db1.lookup(
		    "city/names/en",
		    std.ip("81.2.69.192", "0.0.0.0"));
		set resp.http.country = db1.lookup(
		    "country/names/en",
		    std.ip("2001:218::1", "0.0.0.0"));
		set resp.http.connection-type = db2.lookup(
		    "connection_type",
		    std.ip("1.0.128.1", "0.0.0.0"));
		set resp.http.isp = db3.lookup(
		    "isp",
		    std.ip("1.0.128.1", "0.0.0.0"));
	}
} -start

client c1 {
	txreq
	rxresp
	expect resp.http.city == "London"
	expect resp.http.country == "Japan"
	expect resp.http.connection-type == "Dialup"
	expect resp.http.isp == "TOT Public Company Limited"
} -run
